#!/bin/cyn

# Cyn Standard Library

#? Sum an array of numbers.
#? ## Example
#? ```cyn
#? sum [1, 2, 3, 4] # 10
#? ```
sum = reduce 0 (Î»a b -> $a + $b)

#? Concatentate an array of arrays.
#? ## Example
#? ```cyn
#? concat [[1], [2, 3], [4, 5, 6]] # [1, 2, 3, 4, 5, 6]
#? ```
concat = reduce [] (Î»a b -> $a ++ $b)

#? Reverse an array.
#? ## Example
#? ```cyn
#? reverse [1, 2, 3] # [3, 2, 1]
#? ```
reverse arr = reverse' $arr (len $arr)
reverse' arr i = if $i == 0
    then []
    else [$arr !! ($i - 1)] ++ reverse' $arr ($i - 1)

map f arr = map' $f $arr 0
map' f arr i = if $i == len $arr
    then []
    else [$f ($arr !! ($i))] ++ map' $f $arr ($i + 1)

filter f arr = filter' $f $arr 0
filter' f arr i = if $i == len $arr
    then []
    else (if $f ($arr !! ($i)) then [$arr !! ($i)] else []) ++ filter' $f $arr ($i + 1)

reduce acc f arr = reduce' $acc $f $arr 0
reduce' acc f arr i = if $i == len $arr
    then $acc
    else reduce' ($f $acc ($arr !! $i)) $f $arr ($i + 1)

id x = $x

add x y = $x + $y
sub x y = $x - $y
mul x y = $x * $y
div x y = $x // $y
eq x y = $x == $y
lt x y = $x < $y
gt x y = $x > $y
and x y = $x && $y
or x y = $x || $y

max x y = if $x > $y then $x else $y
min x y = if $x < $y then $x else $y

zip arr1 arr2 = zip' $arr1 $arr2 0
zip' arr1 arr2 i = if $i == len $arr1 || $i == len $arr2
    then []
    else [[$arr1 !! $i, $arr2 !! $i]] ++ zip' $arr1 $arr2 ($i + 1)

